#!/usr/bin/perl

use strict;
use warnings;

use File::Basename;
use POSIX qw(strftime);

my $srcfile = shift;
my @destfiles = @ARGV;

# DEBUGGING
# print "'" . join( "',\n'", ( $srcfile, @destfiles ) ) . "'\n";

unless ( $srcfile && @destfiles ) {
  my $scriptname = basename $0;
  print "CopyDate - copies one file's mtime timestamp to another.\n";
  print "Usage: $scriptname srcfile destfile [destfile2 destfile3 ...]\n\n";
  exit 1;
}

unless ( -e $srcfile ) {
  print "Path does not exist: $srcfile\n";
  exit 1;
}

unless ( grep { -e } @destfiles ) {
  print "Path(s) not found:\n";
  print join( "\n", @destfiles ) . "\n";
  exit 1;
}

@destfiles = grep { -e } @destfiles;

my ( $atime, $mtime ) = (stat($srcfile))[8..9];

foreach my $destfile (@destfiles) {
  if ( utime( $atime, $mtime, $destfile ) ) {
    print strftime( "%Y-%m-%d %H:%M:%S", localtime($mtime) ) . "\n";
  } else {
    print "Failed to set file timestamp on '$destfile': $!\n";
  }
}
